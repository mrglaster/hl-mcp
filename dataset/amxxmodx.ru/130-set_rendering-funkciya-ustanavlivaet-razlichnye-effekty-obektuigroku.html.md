# set_rendering Функция устанавливает различные эффекты объекту/игроку
[The Engine Module](http://amxxmodx.ru/engine/) [engine_stocks.inc](http://amxxmodx.ru/engine/engine_stocksinc/)
Данная функция устанавливает различные эффекты объекту или игроку.  
Эффекты в виде свечения или полу прозрачности и другие, я все не изучал.  
  
Инфо из **engine_stocks.inc:**  
```
/*  Set rendering of an entity */  
stock set_rendering(index, fx=kRenderFxNone, r=255, g=255, b=255, render=kRenderNormal, amount=16)  
{  
    entity_set_int(index,EV_INT_renderfx,fx);  
    new Float:RenderColor[3];  
    RenderColor[0] = float(r);  
    RenderColor[1] = float(g);  
    RenderColor[2] = float(b);  
    entity_set_vector(index,EV_VEC_rendercolor,RenderColor);  
    entity_set_int(index,EV_INT_rendermode,render);  
    entity_set_float(index,EV_FL_renderamt,float(amount));  
  
    return 1;  
}
```
  
  
**Синтаксис:**  

set_rendering ( index, fx=kRenderFxNone, r=255,g=255,b=255, render=kRenderNormal,amount=16 )
  
  
**Тип функции:**  
stock  
  
**Пример:**  
```
/* Plugin generated by AMXX-Studio */  
   
#include <amxmodx>  
#include <amxmisc>  
#include <engine>  
   
#define PLUGIN "[engine.inc] set_rendering"  
#define VERSION "1.0"  
#define AUTHOR "Admin"  
   
new Ent  
new g_Model[] = {"models/chick.mdl"}  
   
public plugin_init() {  
    register_plugin(PLUGIN, VERSION, AUTHOR)  
    register_clcmd("say /rendoff","rendoff")  
    register_clcmd("say /rendon","rendon")  
    register_clcmd("say /chick","add_chicken")  
       
}  
public plugin_precache(){  
    precache_model(g_Model)  
}  
public rendoff(id){      
  
     set_rendering (id,kRenderFxNone,255,255,255,kRenderNormal,16)  
     set_rendering (Ent,kRenderFxNone,255,255,255,kRenderNormal,16)  
}  
public rendon(id){      
    set_rendering( id,kRenderFxGlowShell,0,0,0,kRenderTransAlpha,20)  
    set_rendering( Ent,kRenderFxGlowShell,0,0,0,kRenderTransAlpha,20)  
}  
public add_chicken(id){  
       
    Ent = create_entity("info_target")  
       
    if(is_valid_ent(Ent)){  
           
        entity_set_model(Ent, g_Model)  
        entity_set_size(Ent,Float:{-6.0, -10.0, 0.0},Float:{6.0, 10.0, 18.0})  
        new Float:f_Origin[3]  
        entity_get_vector(id, EV_VEC_origin, f_Origin)      
        f_Origin[0] += 50.0  
        entity_set_origin(Ent, f_Origin)          
        entity_set_int(Ent,EV_INT_solid,SOLID_BBOX)  
           
        entity_set_float(Ent, EV_FL_takedamage, DAMAGE_YES)  
           
        new Float:angles[3]  
        angles[0] = 0.0  
        angles[1] = 0.0  
        angles[2] = 0.0  
        entity_set_vector(Ent,EV_VEC_angles,angles)  
           
        drop_to_floor(Ent)  
    }  
}
```
  
  
**Описание:**  
Функция аналогична функции из модуля **FUN** [**set_user_rendering**](http://amxxmodx.ru/fun/24-set_user_rendering.html)  
В этом примере же создан объект в виде курицы, на котором так же будет виден эффект как и на игроке.  
Эффект в виде 20 процентной видимости объекта, над которым выполнялась функция.
